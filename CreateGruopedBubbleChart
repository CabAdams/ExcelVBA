Public Sub CreateGruopedBubbleChart()
    If (Selection.Columns.Count <> 5 Or Selection.Rows.Count < 3) Then
        MsgBox "Selection must have 5 columns and at least 2 rows"
        Exit Sub
    End If
    
  
    Dim bubbleChart As ChartObject
    Set bubbleChart = ActiveSheet.ChartObjects.Add(Left:=Selection.Left, Width:=18.92 * 72 / 2.54 * 1.169629, Top:=Selection.Top, Height:=6.8 * 72 / 2.54 * 1.102362)
    bubbleChart.Chart.ChartType = xlBubble
    Dim r As Integer
    For r = 2 To Selection.Rows.Count
        With bubbleChart.Chart.SeriesCollection.NewSeries
            .Name = "=" & Selection.Cells(r, 1).Address(External:=True)
            .XValues = Selection.Cells(r, 2).Address(External:=True)
            .Values = Selection.Cells(r, 3).Address(External:=True)
            .BubbleSizes = Selection.Cells(r, 4).Address(External:=True)
            .Interior.Color = Selection.Cells(r, 5).Interior.Color
            .ApplyDataLabels
            .DataLabels.ShowSeriesName = True
            .DataLabels.ShowCategoryName = True
            .DataLabels.Separator = "" & Chr(10) & ""
            .DataLabels.Position = xlLabelPositionRight
        End With

    Next
    
    With bubbleChart.Chart
        .Axes(xlValue).HasMajorGridlines = False
        .HasLegend = False
        .ChartArea.Border.LineStyle = xlNone
        .ChartArea.Interior.ColorIndex = xlNone
        .PlotArea.Interior.ColorIndex = xlNone
        .ChartArea.Font.Size = 7
        .ChartArea.Font.Name = "Arial"
        .SetElement (msoElementPrimaryCategoryAxisTitleAdjacentToAxis)
        .Axes(xlCategory, xlPrimary).AxisTitle.Text = "=" & Selection.Cells(1, 2).Address(External:=True)
        .SetElement (msoElementPrimaryValueAxisTitleRotated)
        .Axes(xlValue, xlPrimary).AxisTitle.Text = "=" & Selection.Cells(1, 3).Address(External:=True)
        .Axes(xlCategory).MinimumScale = 0
        .Parent.Placement = xlMove
    End With
End Sub
